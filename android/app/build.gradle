def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

//Facebook configuration

def appSettings = new Properties()
def appSettingsFile = rootProject.file('appconfig.properties')
if (appSettingsFile.exists()) {
    appSettingsFile.withReader('UTF-8') { reader ->
        appSettings.load(reader)
    }
}
def facebookAppId = appSettings.getProperty('FACEBOOK_APP_ID')
if(facebookAppId == null){
    throw new GradleException("Facebook App Id not found")
}

def facebookClientToken = appSettings.getProperty('FACEBOOK_CLIENT_TOKEN')
if(facebookClientToken == null){
    throw new GradleException("Facebook Client Token not found")
}

def env = appSettings.getProperty('ENV')

apply plugin: 'com.android.application'
// START: FlutterFire Configuration
apply plugin: 'com.google.gms.google-services'
// END: FlutterFire Configuration
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.theseedsoft.hatspace"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-build-configuration.
        minSdkVersion 21
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
        //Facebook configuration
        resValue 'string','facebook_app_id',facebookAppId
        resValue 'string','facebook_client_token',facebookClientToken
        resValue 'string', 'fb_login_protocol_scheme',"fb$facebookAppId}"
        resValue 'string', 'app_name', 'HATSpace'
        manifestPlaceholders.appIcon = 'ic_launcher'
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            resValue 'string', 'app_name', 'HATSpace Debug'
            manifestPlaceholders.appIcon = 'ic_launcher'
        }

        release {
            signingConfig signingConfigs.release

            if (env == null) {
                manifestPlaceholders.appIcon = 'ic_launcher'
                resValue 'string', 'app_name', 'HATSpace'
            } else {
                resValue 'string', 'app_name', "HATSpace $env"
                applicationIdSuffix ".$env"
                manifestPlaceholders.appIcon = "ic_launcher_$env"
            }
        }
    }
}

flutter {
    source '../..'
}



dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.facebook.android:facebook-android-sdk:latest.release'
}
